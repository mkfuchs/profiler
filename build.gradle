version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    //graphstream was pulling in lowagie itext which pulled in some
    //ancient bouncy castle stuff, breaking during runtime.
    //neither itext nor bouncycastle appear to be necessary.
    def withoutBc = { exclude group: 'com.lowagie', module: 'itext'}
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.27.2.1'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.6'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile group: 'com.google.guava', name: 'guava', version: '27.1-jre'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.8'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.9.8'
    compile group: 'org.graphstream', name: 'gs-core', version: '1.3', withoutBc
    compile group: 'org.graphstream', name: 'gs-algo', version: '1.3' , withoutBc
    compile group: 'org.graphstream', name: 'gs-ui', version: '1.3' , withoutBc
    compile group: 'net.agkn', name: 'hll', version: '1.6.0'
}

jar {
    manifest {
        attributes "Main-Class": "com.mkf.profiler.Main"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task makeShellScript(type: Exec, dependsOn: jar) {
    workingDir '.'
    print(workingDir)
    commandLine 'cat', 'src/main/scripts/stub.sh', 'build/libs/csvprofiler-1.0-SNAPSHOT.jar'
    doFirst {
        standardOutput new FileOutputStream("${buildDir}/profiler.sh")
    }

}


